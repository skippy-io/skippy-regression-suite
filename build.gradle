buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    }
    dependencies {
        classpath 'io.skippy:skippy-gradle:' + project.skippyVersion
        classpath 'io.skippy:skippy-repository-regression-suite:'  + project.skippyVersion
    }
}

plugins {
    id 'java'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
    implementation 'io.skippy:skippy-core:' + skippyVersion
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.skyscreamer:jsonassert:1.5.1'
    testImplementation gradleTestKit()
}

task copyProjectsToResourcesFolder(type: Copy) {
    from 'test-projects'
    into 'src/test/resources/test-projects'
}

task processBuildGradle {
    doLast {
        fileTree(dir: '.', include: ['src/test/resources/test-projects/**/build.gradle']).each { File file ->
            var content = file.text
            content = content.replaceAll('onlyIf \\{ false \\}', 'onlyIf \\{ true \\}')
            content = content.replaceAll('project.skippyVersion', "'${project.skippyVersion}'")
            content = content.replaceAll('project.junit4Version', "'${project.junit4Version}'")
            content = content.replaceAll('project.junit5Version', "'${project.junit5Version}'")
            file.write(content)

            File settingsFile = new File(file.parent, "settings.gradle")
            if (!settingsFile.exists()) {
                settingsFile.createNewFile()
            }

        }
        fileTree(dir: '.', include: ['src/test/resources/test-projects/**/pom.xml']).each { File file ->
            var content = file.text
            content = content.replaceAll('project.skippyVersion', project.skippyVersion)
            content = content.replaceAll('project.junit4Version', project.junit4Version)
            content = content.replaceAll('project.junit5Version', project.junit5Version)
            file.write(content)
        }
    }
}

clean {
    doLast {
        new File('src/test/resources/test-projects/').deleteDir()
    }
}

processBuildGradle.dependsOn copyProjectsToResourcesFolder
processTestResources.dependsOn processBuildGradle

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
    useJUnitPlatform {
        excludeTags "GRADLE", "MAVEN"
    }
}

def gradleTest = tasks.register("gradleTest", Test) {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat 'FULL'
    }
    useJUnitPlatform {
        includeTags "GRADLE"
    }
}

def mavenTest = tasks.register("mavenTest", Test) {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat 'FULL'
    }
    useJUnitPlatform {
        includeTags "MAVEN"
    }
    dependsOn skippyClean
    shouldRunAfter test
}